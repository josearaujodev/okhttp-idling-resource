buildscript {
  rootProject.ext.agpVersion = findProperty("agpVersion")?.toString() ?: "7.4.2"
  rootProject.ext.kotlinVersion = findProperty("kotlinVersion")?.toString() ?: "1.8.10"
  rootProject.ext.okHttpVersion = findProperty("okHttpVersion")?.toString() ?: "4.10.0"

  dependencies {
    classpath "com.android.tools.build:gradle:${agpVersion}"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
  }
}

plugins {
  id 'com.android.library' version "${agpVersion}"
  id 'org.jetbrains.kotlin.android' version "${kotlinVersion}"
}

dependencies {
  compileOnly "com.squareup.okhttp3:okhttp:${okHttpVersion}"
  api 'androidx.test.espresso:espresso-idling-resource:3.5.1'
  implementation 'androidx.annotation:annotation:1.6.0'

  testImplementation 'junit:junit:4.13.2'
  testImplementation 'com.google.truth:truth:1.1.3'
  testImplementation "com.squareup.okhttp3:mockwebserver:${okHttpVersion}"
}

android {

  compileSdkVersion 33

  defaultConfig {
    minSdkVersion 14
    targetSdkVersion 33
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  kotlinOptions {
    jvmTarget = '1.8'
  }

  lintOptions {
    textReport true
    textOutput 'stdout'
    ignore 'InvalidPackage'
  }
}

// use chrisbanes mvn push script, but lock it to a specific commit, to avoid automatic "updates"
apply from: 'https://raw.githubusercontent.com/chrisbanes/gradle-mvn-push/b9d34d928d772c0acc2e0981fc94c6e98b9038db/gradle-mvn-push.gradle'
